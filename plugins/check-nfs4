#!/usr/bin/perl

=head1 NAME

check-nfs4 - Plugin to check NFS v4 server statistics.

=head1 SYNOPSIS

    check-nfs4 [ OPTIONS ]

    check-nfs4 --help

=head1 REPORTING BUGS

Please report all bugs to <support(at)bloonix.de>.

=head1 AUTHOR

Jonny Schulz <support(at)bloonix.de>.

=head1 POWERED BY

     _    __ _____ _____ __  __ __ __   __
    | |__|  |     |     |  \|  |__|\  \/  /
    |  . |  |  |  |  |  |      |  | >    <
    |____|__|_____|_____|__|\__|__|/__/\__\

=head1 COPYRIGHT

Copyright (C) 2012-2014 by Jonny Schulz. All rights reserved.

=cut

use strict;
use warnings;
use Bloonix::Plugin;

# --------------------------------------------------
# Plugin options
# --------------------------------------------------

my $plugin = Bloonix::Plugin->new(version => "0.4");

$plugin->has_threshold(
    keys => [qw(
        access close commit create delegpurge delegreturn getattr getfh link lock
        lockt locku lookup lookupp nverify open openattr open_confirm open_downgrade
        putfh putpubfh putrootfh read readdir readlink remove rename renew restorefh
        savefh secinfo setattr setclientid setclientid_confirm verify write rellockowner
    )]
);

# --------------------------------------------------
# Parse options
# --------------------------------------------------

my $opt = $plugin->parse_options;
my $procnfs  = "/proc/net/rpc/nfsd";

# --------------------------------------------------
# Load statistics
# --------------------------------------------------

sub load {
    my $file = shift;
    my %stat = ();

    open my $fh, "<", $file or do {
        $plugin->exit(
            status => "CRITICAL",
            message => "nfs4 server: unable to open '$file' - $!"
        );
    };

    while (my $line = <$fh>) {
        if ($line =~ /^proc4ops\s+(.*)/) {
            @stat{qw(
                access close commit create delegpurge delegreturn getattr getfh link lock
                lockt locku lookup lookupp nverify open openattr open_confirm open_downgrade
                putfh putpubfh putrootfh read readdir readlink remove rename renew restorefh
                savefh secinfo setattr setclientid setclientid_confirm verify write rellockowner
            )} = split /\s+/, $1;
        }
    }

    return \%stat;
}

# --------------------------------------------------
# Load initial statistics
# --------------------------------------------------

my $init = $plugin->load_json;
my $time = undef;

if (defined $init) {
    $time = delete $init->{time};
} else {
    $init = &load($procnfs);
    $time = time;
    sleep 1;
}

# --------------------------------------------------
# Load current statistics
# --------------------------------------------------

my $stat  = &load($procnfs);
my $ntime = time;
my $delta = $ntime - $time;

# --------------------------------------------------
# Calculate deltas
# --------------------------------------------------

foreach my $key (keys %$stat) {
    my $value = $stat->{$key};

    if ($stat->{$key} == $init->{$key} || $init->{$key} > $stat->{$key}) {
        $stat->{$key} = sprintf("%.2f", 0);
    } elsif ($delta > 0) {
        $stat->{$key} = sprintf("%.2f", ($stat->{$key} - $init->{$key}) / $delta);
    } else {
        $stat->{$key} = sprintf("%.2f", $stat->{$key} - $init->{$key});
    }

    $init->{$key} = $value;
}

# --------------------------------------------------
# Store statistics for the next run
# --------------------------------------------------

$init->{time} = $ntime;
$plugin->safe_json($init);

# --------------------------------------------------
# Check warning and critical
# --------------------------------------------------

my $result = $plugin->check_thresholds(
    stats => $stat,
    upshot_keys => [qw(getattr setattr read write)]
);

$plugin->exit(
    status => $result->{status},
    message => "nfs4 server: $result->{upshot}",
    stats => $stat
);
